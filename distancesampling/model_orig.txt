
model {

  beta0 ~ dnorm(0, 0.01)  
  mean.lam <- exp(beta0)

  beta1 ~ dnorm(0, 0.01) # DC this is now the road density coefficeint  
  beta2 ~ dnorm(0, 0.01) # DC add another prior for land coeffient
  phi ~ dunif(0,1)        
  sigma ~ dunif(0.01,5)   
  gamma0 ~ dnorm(0, 0.01) # DC add a prior for groupsize intercept
  gamma1 ~ dnorm(0, 0.01) # DC add a prior road density coefficient on groupsize
  gamma2 ~ dnorm(0, 0.01) # DC add another prior for land coefficent on groupsize

  for(b in 1:nD){
    log(g[b]) <- -midpt[b]*midpt[b]/(2*sigma*sigma) 
    f[b] <- delta/B     #DC change to uniform density function for line transects

    cellprobs[b] <- g[b]*f[b]
    cellprobs.cond[b] <- cellprobs[b]/sum(cellprobs[1:nD])
  }
  cellprobs[nD+1]<- 1-sum(cellprobs[1:nD])
  for (s in 1:nsites) {
    for (k in 1:nreps[s]) { # We cannot loop up to K for each site because most sites were not surveyed k times. We must only loop up to the site-specific maximum number of replicates conducted.  you need to add a reference to a value that is specific to each site here.
    
      pdet[s,k] <- sum(cellprobs[1:nD])   
      pmarg[s,k] <- pdet[s,k]*phi
      y3d[s,1:nD,k] ~ dmulti(cellprobs.cond[1:nD], nobs[s,k])
      nobs[s,k] ~ dbin(pmarg[s,k], M[s])

    }  
    for (k in 1:K){

        Navail[s,k] ~ dbin(phi, M[s])  #move Navail from the limited k (above) to full k loop over K=6 replicates at each site
    }

    M[s] ~ dpois(lambda[s])
    log(lambda[s]) <- log(area[s]) + beta0 + beta1*roads[s] + beta2*land[s]  #DC add roads and land and offset term for search area size
    log(gamma[s]) <- gamma0 + gamma1*roads[s] + gamma2*land[s] #DC add GLM for expected group size at each site
  }  
  
  for(i in 1:ngroup){
    groupsize[i] ~ dpois(gamma[site[i]]) # need to add in a new loop over all groups

  }
  
  Mtot <- sum(M[])
  for(k in 1:K){
    Ntot[k]<- sum(Navail[,k])

    D[k]<-sum(Navail[,k]*gamma[])/(2*B*434.8) # Density across the entire study area in each replicate, numerator is total number of individuals. What goes in the denominator? you will need to know the total length of all sites is 434.8 km to put the correct area  in the denominator

  }
  Davg<-mean(D[])  # overall average density of pronghorn across all replicates
  
} 
